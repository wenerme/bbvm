package parser

import "github.com/wenerme/bbvm/bbasm"

type BBAsm Peg {
	parser
}

#-------------------------------------------------------------------------
# Instructions
#-------------------------------------------------------------------------
Start <- ( Spacing Assembly? NL{p.line++} )* EOT
	Literal? # Unused

Assembly
	<- (  Comment
		/ Label
        / Inst
        / Pseudo ){p.AddAssembly()} (Comment {p.AddAssembly();p.AddComment()})?

Comment
	<- SEMICOLON <(!NL .)*> {p.Push(&Comment{});p.Push(text)}
Label
	<- {p.Push(&Label{})} Identifier Spacing COLON

Inst
	<-  EXIT
		/ RET
		/ NOP
        / ( CALL / PUSH / POP / JMP ) Operand
        / ( IN / OUT ) Operand COMMA Operand
        / CAL	<DATA_TYPE>{p.Push(lookup(bbasm.INT,text))} <CAL_OP>{p.Push(lookup(bbasm.ADD,text))} Operand COMMA Operand
        / LD	<DATA_TYPE>{p.Push(lookup(bbasm.INT,text))} Operand COMMA Operand
        / CMP	<DATA_TYPE>{p.Push(lookup(bbasm.INT,text))} Operand COMMA Operand
        / JPC	<CMP_OP>{p.Push(lookup(bbasm.A,text))}  Operand
Pseudo <- BLOCK IntegerLiteral IntegerLiteral
		/ DATA Identifier PSEUDO_DATA_TYPE? PseudoDataValue ( COMMA PseudoDataValue )*

PseudoDataValue
	<-	IntegerLiteral	{p.AddPseudoDataValue()}
	/	Identifier		{p.AddPseudoDataValue()}
	/	StringLiteral	{p.AddPseudoDataValue()}
	/	<'%' HexDigits '%'> Spacing {p.Push(text);p.AddPseudoDataValue()}

# Compatible with old version
PSEUDO_DATA_TYPE
	<- DATA_TYPE
	 / ( "CHAR" / "BIN" ) Space

Operand <- (Identifier 					{p.AddOperand(true)}
        / LBRK Identifier RBRK 			{p.AddOperand(false)}
        / IntegerLiteral 				{p.AddOperand(true)}
        / LBRK IntegerLiteral RBRK		{p.AddOperand(false)}
        ) Spacing

#=========================================================================
#  Lexical Structure
#=========================================================================
#-------------------------------------------------------------------------
#  Spacing
#-------------------------------------------------------------------------
Spacing        	<-  [ \t\r\f]*
Space           <-  [ \t\r\f]+

#-------------------------------------------------------------------------
#  Identifiers
#-------------------------------------------------------------------------
Identifier 		<- <Letter LetterOrDigit*> Spacing {p.Push(text)}
Letter 			<- [a-z] / [A-Z] / [_$]
LetterOrDigit 	<- [a-z] / [A-Z] / [0-9] / [_$]

#-------------------------------------------------------------------------
# Keyword
#-------------------------------------------------------------------------

EXIT 	<- "EXIT" 	Spacing	{p.PushInst(bbasm.EXIT)}
RET 	<- "RET" 	Spacing {p.PushInst(bbasm.RET)}
NOP 	<- "NOP" 	Spacing {p.PushInst(bbasm.NOP)}
CALL 	<- "CALL" 	Space 	{p.PushInst(bbasm.CALL)}
PUSH 	<- "PUSH" 	Space 	{p.PushInst(bbasm.PUSH)}
POP 	<- "POP" 	Space 	{p.PushInst(bbasm.POP)}
JMP 	<- "JMP" 	Space 	{p.PushInst(bbasm.JMP)}
IN 		<- "IN" 	Space 	{p.PushInst(bbasm.IN)}
OUT 	<- "OUT" 	Space 	{p.PushInst(bbasm.OUT)}
CAL 	<- "CAL" 	Space 	{p.PushInst(bbasm.CAL)}
LD 		<- "LD" 	Space 	{p.PushInst(bbasm.LD)}
CMP 	<- "CMP" 	Space 	{p.PushInst(bbasm.CMP)}
JPC 	<- "JPC" 	Space 	{p.PushInst(bbasm.JPC)}

BLOCK 	<- ".BLOCK" Space	{p.Push(&PseudoBlock{})}
DATA 	<- "DATA" 	Space	{p.Push(&PseudoData{})}

CAL_OP 			<- 	( "ADD" / "SUB" / "MUL" / "DIV" / "MOD" ) 		Space
CMP_OP 			<-	( "Z" / "BE" / "B" / "AE" / "A" / "NZ" ) 		Space
DATA_TYPE       <-  ( "DWORD" / "WORD" / "BYTE" / "FLOAT" / "INT" ) Space

#-------------------------------------------------------------------------
# Separators, Operators
#-------------------------------------------------------------------------
LBRK            <-  '['        	Spacing
#LPAR           <-  '('        	Spacing
RBRK            <-  ']'        	Spacing
#RPAR           <-  ')'        	Spacing
COMMA           <-  ','        	Spacing
SEMICOLON		<- 	";" 		Spacing
COLON			<- 	':'			Spacing
MINUS			<-	'-' 		Spacing

NL              <-  '\n'
EOT             <-  !.

#-------------------------------------------------------------------------
# Literals
#-------------------------------------------------------------------------

Literal
   <- ( FloatLiteral
      / IntegerLiteral          # May be a prefix of FloatLiteral
      / CharLiteral
      / StringLiteral
      ) Spacing

IntegerLiteral
   <- <MINUS?
	( HexNumeral
	/ BinaryNumeral
	/ OctalNumeral            # May be a prefix of HexNumeral or BinaryNumeral
	/ DecimalNumeral          # May be a prefix of OctalNumeral
	)> Spacing{p.Push(text);p.AddInteger()}

DecimalNumeral <- '0' / [1-9] ([_]* [0-9])*

HexNumeral     <- ('0x' / '0X') HexDigits

BinaryNumeral  <- ('0b' / '0B') [01] ([_]* [01])*

OctalNumeral   <- '0' ([_]* [0-7])+

FloatLiteral   <- HexFloat / DecimalFloat

DecimalFloat
	<- Digits '.' Digits?  Exponent?
	/ '.' Digits Exponent?
	/ Digits Exponent?

Exponent <- [eE] [+\-]? Digits

HexFloat <- HexSignificand BinaryExponent

HexSignificand
   <- ('0x' / '0X') HexDigits? '.' HexDigits
    / HexNumeral '.'?                           # May be a prefix of above

BinaryExponent <- [pP] [+\-]? Digits

Digits <- [0-9]([_]*[0-9])*

HexDigits <- HexDigit ([_]*HexDigit)*

HexDigit <- [a-f] / [A-F] / [0-9]

CharLiteral <- ['] (Escape / !['\\] .) [']

StringLiteral <- <'\"' (Escape / !["\\\n\r] .)* '\"'> {p.Push(text)}

Escape <- '\\' ([btnfr"'\\] / OctalEscape / UnicodeEscape)

OctalEscape
   <- [0-3][0-7][0-7]
    / [0-7][0-7]
    / [0-7]

UnicodeEscape
   <- 'u'+ HexDigit HexDigit HexDigit HexDigit

